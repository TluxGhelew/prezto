###############################################################################
# File:     web-search
# Version   0.1
# Author:   Robby Russel <http://github.com/robbyrussel>
# Author:   Thierry Ghelew <tlux@ghelew.net>
# Licence:  MIT
###############################################################################
# Adapted Robby Russel's web-seach plugin to fit into zprezto 

# define search engine URLS
typeset -A urls
urls=(
duckduckgo  "https://www.duckduckgo.com/?q="
qwant       "https://lite.qwant.com/?q="
google      "https://www.google.com/search?q="
bing        "https://www.bing.com/search?q="
yahoo       "https://search.yahoo.com/search?p="
yandex      "https://yandex.ru/yandsearch?text="
github      "https://github.com/search?q="
)

# define the open command
case "$OSTYPE" in
    darwin*)  open_cmd="open" ;;
    cygwin*)  open_cmd="cygstart" ;;
    linux*)   open_cmd="xdg-open" ;;
    *)        echo "Platform $OSTYPE not supported"
        return 1
        ;;
esac

# check whether the search engine is supported
if [[ -z "$urls[$1]" ]]; then
    echo "Search engine $1 not supported."
    return 1
fi

# search or go to main page depending on number of arguments passed
if [[ $# -gt 1 ]]; then
    # build search url:
    # join arguments passed with '+', then append to search engine URL
    url="${urls[$1]}${(j:+:)@[2,-1]}"
else
    # build main page url:
    # split by '/', then rejoin protocol (1) and domain (2) parts with '//'
    url="${(j://:)${(s:/:)urls[$1]}[1,2]}"
fi

nohup $open_cmd "$url" &>/dev/null
